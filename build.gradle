plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
}

group 'io.openliberty.tools'
version '0.0.7'

sourceCompatibility = 11

repositories {
    mavenCentral()
    maven {
        url 'https://repo.eclipse.org/content/repositories/lsp4mp-snapshots'
    }
    maven {
        url 'https://repo.eclipse.org/content/repositories/lsp4mp-releases'
    }
    maven {
        url 'https://repo.eclipse.org/content/repositories/lemminx-releases'
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenLocal() // TODO remove once Liberty LS is publicly available
}

configurations {
    lsp
    // needed to avoid ClassCastException org.apache.xerces.jaxp.DocumentBuilderFactoryImpl cannot be cast to class javax.xml.parsers.DocumentBuilderFactory
    // xml-apis interfering with xercs
    all*.exclude group: 'xml-apis'
}

dependencies {
    // LSP4IJ dependencies
    implementation 'org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.5.0'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.15.0'
    implementation 'org.eclipse.lemminx:org.eclipse.lemminx:0.22.0'
    implementation 'io.openliberty.tools:liberty-langserver-lemminx:1.0-SNAPSHOT'
    implementation 'io.openliberty.tools:liberty-langserver:1.0-SNAPSHOT'
    implementation 'org.eclipse.lsp4jakarta:org.eclipse.lsp4jakarta.ls:0.0.1-SNAPSHOT'
    //required by lsp4j as the version from IJ is incompatible
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.vladsch.flexmark:flexmark:0.50.50'
    implementation 'org.jsoup:jsoup:1.14.2'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'org.apache.maven:maven-artifact:3.6.3'

    lsp('org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.5.0:uber') {
        transitive = false
    }
    lsp('org.eclipse.lemminx:org.eclipse.lemminx:0.22.0:uber') {
            transitive = false
    }
    lsp('io.openliberty.tools:liberty-langserver-lemminx:1.0-SNAPSHOT:jar-with-dependencies') {
        transitive = false
    }
    lsp('io.openliberty.tools:liberty-langserver:1.0-SNAPSHOT:jar-with-dependencies') {
        transitive = false
    }
    lsp('org.eclipse.lsp4jakarta:org.eclipse.lsp4jakarta.ls:0.0.1-SNAPSHOT:jar-with-dependencies') {
        transitive = false
    }
    implementation files(new File(buildDir, 'server')) {
        builtBy 'copyDeps'
    }
}

task copyDeps(type: Copy) {
    from configurations.lsp
    into new File(buildDir, 'server/server')
    rename '^(.*)(-[0-9]+.[0-9]+.[0-9]+(-SNAPSHOT)?)(.*)$', '$1$4'
}

runIde {
    jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
}


task buildJakartaCore(type: Exec) {
    workingDir "../lsp4jakarta/jakarta.jdt/org.eclipse.lsp4jakarta.jdt.core"
    commandLine 'mvn', 'clean', 'install'
}

task buildJakartaLs(type: Exec) {
    workingDir "../lsp4jakarta/jakarta.ls"
    commandLine 'mvn',  'clean', 'install'
}

task buildJakarta {
    dependsOn 'buildJakartaCore'
    dependsOn 'buildJakartaLs'
    tasks.findByName('buildJakartaLs').mustRunAfter 'buildJakartaCore'
}

intellij {
    version = '2021.1.3' //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    plugins = ['java', 'maven', 'gradle-java', 'properties', 'terminal']
    updateSinceUntilBuild = false
}

patchPluginXml {
    changeNotes """
        <b> 0.0.7 </b>
        <ul>
        <li> Rename plugin to Liberty Tools for IntelliJ
        </ul>
        <b> 0.0.6 </b>
        <ul>
        <li> Fix release
        </ul>
        <b> 0.0.5 </b>
        <ul>
        <li> Update action wording and documentation
        </ul>
        <b> 0.0.4 </b>
        <ul>
        <li> Removed tech preview wording from "start in container" action
        </ul>
        <b> 0.0.3 </b>
        <ul>
        <li> Updated plugin description
        </ul>
        <b> 0.0.2 </b>
        <ul>
        <li> Added start in container (tech preview) action
        </ul>
        <b> 0.0.1 </b>
        <ul>
        <li> First preview release
        </ul>
        """
    version project.version
}