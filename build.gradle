plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
}

group 'io.openliberty.tools'
version '0.0.7'

sourceCompatibility = 11

repositories {
    mavenLocal() // TODO remove once Liberty LS is publicly available
    mavenCentral()
    maven {
        url 'https://repo.eclipse.org/content/repositories/lsp4mp-snapshots'
    }
    maven {
        url 'https://repo.eclipse.org/content/repositories/lsp4mp-releases'
    }
    maven {
        url 'https://repo.eclipse.org/content/repositories/lemminx-releases'
    }
}

configurations {
    lsp
}

dependencies {
    // LSP4IJ dependencies
    compile 'org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.5.0'
    compile 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.14.0'
    compile 'org.eclipse.lemminx:org.eclipse.lemminx:0.20.0'
    compile 'io.openliberty.tools:liberty-langserver-lemminx:1.0-SNAPSHOT'
    //required by lsp4j as the version from IJ is incompatible
    compile 'com.google.code.gson:gson:2.8.9'
    compile 'com.vladsch.flexmark:flexmark:0.50.50'
    compile 'com.kotcrab.remark:remark:1.2.0'
    compile 'org.jsoup:jsoup:1.14.2'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'org.apache.maven:maven-artifact:3.6.3'

    lsp('org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.5.0:uber') {
        transitive = false
    }
    lsp('org.eclipse.lemminx:org.eclipse.lemminx:0.20.0:uber') {
        transitive = false
    }
    lsp('io.openliberty.tools:liberty-langserver-lemminx:1.0-SNAPSHOT:jar-with-dependencies') {
        transitive = false
    }
    compile files(new File(buildDir, 'server')) {
        builtBy 'copyDeps'
    }
}

task copyDeps(type: Copy) {
    from configurations.lsp
    into new File(buildDir, 'server/server')
    rename '^(.*)(-[0-9]+.[0-9]+.[0-9]+(-SNAPSHOT)?)(.*)$', '$1$4'
}

runIde {
    jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
}

intellij {
    version = '2020.2.4' //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    plugins = ['java', 'maven', 'gradle-java', 'properties', 'terminal']
    updateSinceUntilBuild = false
}

patchPluginXml {
    changeNotes """
        <b> 0.0.7 </b>
        <ul>
        <li> Rename plugin to Liberty Tools for IntelliJ
        </ul>
        <b> 0.0.6 </b>
        <ul>
        <li> Fix release
        </ul>
        <b> 0.0.5 </b>
        <ul>
        <li> Update action wording and documentation
        </ul>
        <b> 0.0.4 </b>
        <ul>
        <li> Removed tech preview wording from "start in container" action
        </ul>
        <b> 0.0.3 </b>
        <ul>
        <li> Updated plugin description
        </ul>
        <b> 0.0.2 </b>
        <ul>
        <li> Added start in container (tech preview) action
        </ul>
        <b> 0.0.1 </b>
        <ul>
        <li> First preview release
        </ul>
        """
    version project.version
}