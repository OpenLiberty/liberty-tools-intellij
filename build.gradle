plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.13.3'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
}

group 'io.openliberty.tools'
version '23.0.7-SNAPSHOT'

sourceCompatibility = 17
targetCompatibility = 17

def remoteRobotVersion = "0.11.18"

repositories {
    mavenCentral()
    maven {
        url 'https://repo.eclipse.org/content/repositories/snapshots'
    }
    maven {
        url 'https://repo.eclipse.org/content/repositories/releases'
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url = "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
    }
    maven {
        url = "https://www.jetbrains.com/intellij-repository/releases"
    }
    maven {
        url = "https://packages.jetbrains.team/maven/p/kpm/public"
    }
    maven {
        url = "https://cache-redirector.jetbrains.com/packages.jetbrains.team/maven/p/grazi/grazie-platform-public"
    }
    maven {
        url = "https://download.jetbrains.com/teamcity-repository"
    }
    maven {
        url = "https://cache-redirector.jetbrains.com/download-pgp-verifier"
    }

    mavenLocal() // TODO remove once Liberty LS is publicly available
}

configurations {
    lsp
    // needed to avoid ClassCastException org.apache.xerces.jaxp.DocumentBuilderFactoryImpl cannot be cast to class javax.xml.parsers.DocumentBuilderFactory
    // xml-apis interfering with xercs
    all*.exclude group: 'xml-apis'

    runtimeClasspath {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
}

dependencies {
    // LSP4IJ dependencies
    implementation 'org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.6.0'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.15.0'
    implementation 'org.eclipse.lemminx:org.eclipse.lemminx:0.25.0'
    implementation 'io.openliberty.tools:liberty-langserver-lemminx:2.0'
    implementation 'io.openliberty.tools:liberty-langserver:2.0'
    implementation 'org.eclipse.lsp4jakarta:org.eclipse.lsp4jakarta.ls:0.1.1'
    //required by lsp4j as the version from IJ is incompatible
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.vladsch.flexmark:flexmark:0.50.50'
    implementation 'org.jsoup:jsoup:1.15.3'
    //Add junit dependency back when tests are added
    //testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    implementation 'org.apache.maven:maven-artifact:3.6.3'

    // Test: basics.
    testImplementation 'com.intellij.remoterobot:remote-robot:' + remoteRobotVersion
    testImplementation 'com.intellij.remoterobot:remote-fixtures:' + remoteRobotVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation('com.jetbrains.intellij.maven:maven-test-framework:231.9011.34') {
        transitive = false
    }
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.9.2'

    // JUnit 4
    testImplementation 'org.junit.platform:junit-platform-launcher:1.9.3'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.3'

    // Test: Logging Network Calls.
    testImplementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    // Test: Video Recording.
    testImplementation 'com.automation-remarks:video-recorder-junit5:2.0'

    lsp('org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.6.0:uber') {
        transitive = false
    }
    lsp('org.eclipse.lemminx:org.eclipse.lemminx:0.25.0:uber') {
        transitive = false
    }
    lsp('io.openliberty.tools:liberty-langserver-lemminx:2.0:jar-with-dependencies') {
        transitive = false
    }
    lsp('io.openliberty.tools:liberty-langserver:2.0:jar-with-dependencies') {
        transitive = false
    }
    lsp('org.eclipse.lsp4jakarta:org.eclipse.lsp4jakarta.ls:0.1.1:jar-with-dependencies') {
        transitive = false
    }
    implementation files(new File(buildDir, 'server')) {
        builtBy 'copyDeps'
    }
}

task copyDeps(type: Copy) {
    from configurations.lsp
    into new File(buildDir, 'server/server')
    rename '^(.*)(-[0-9]+.[0-9]+.[0-9]+(-SNAPSHOT)?)(.*)$', '$1$4'
}

runIde {
    jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
}

intellij {
    // For a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    version = '2023.1.2'

    plugins = ['java', 'maven', 'gradle-java', 'properties', 'terminal', 'org.jetbrains.idea.maven', 'com.intellij.gradle']
    updateSinceUntilBuild = false
}

downloadRobotServerPlugin {
    version.set(remoteRobotVersion)
}

runIdeForUiTests {
    systemProperty "robot-server.port", "8082"
    systemProperty "ide.mac.message.dialogs.as.sheets", "false"
    systemProperty "jb.privacy.policy.text", "<!--999.999-->"
    systemProperty "idea.trust.all.projects", "true"
    systemProperty "ide.show.tips.on.startup.default.value", "false"
    systemProperty "ide.mac.file.chooser.native", "false"
    systemProperty "jb.consents.confirmation.enabled", "false"
    systemProperty "jbScreenMenuBar.enabled", "false"
    systemProperty "apple.laf.useScreenMenuBar", "false"
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

tasks {
    // Disabling buildSearchableOptions, which runs as part of the buildPlugin task.
    // this is to buildSearchableOptions to workaround the "Only one instance of IDEA can be run at a time" problem.
    // Per documentation: https://plugins.jetbrains.com/docs/intellij/ide-development-instance.html#the-development-instance-sandbox-directory
    buildSearchableOptions {
        enabled = false
    }
}

patchPluginXml {
    changeNotes = """
        <h2> 23.0.6 </h2>
        Version 23.0.6 of Liberty Tools for IntelliJ IDEA contains new functionality and fixes. Version 23.0.6 requires IntelliJ IDEA version 2023.1+ and requires Java 17 or later.
        <br>
        Notable enhancements:
        <ul>
        <li> Diagnostic and hover support when editing server.xml, server.env, bootstrap.properties via the <a href="https://github.com/OpenLiberty/liberty-language-server">Liberty Config Language Server</a>
        <li> Quick fix support when editing server.xml via the <a href="https://github.com/OpenLiberty/liberty-language-server">Liberty Config Language Server</a>
        <li> Diagnostic and hover support for Jakarta EE Web Profile 9.x APIs when editing Java files via the <a href="https://github.com/eclipse/lsp4jakarta">Eclipse LSP4Jakarta</a>, Language Server for Jakarta EE
        <li> Diagnostic, hover and quick fix support for MicroProfile APIs when editing Java and microprofile-config.properties files via the <a href="https://github.com/eclipse/lsp4mp">Eclipse LSP4MP</a>, Language Server for MicroProfile
        <li> Bug fixes to clean up exceptions and improve overall quality of loading projects in the Liberty tool window and running Liberty actions through the Liberty tool window and Search Everywhere window
        </ul>
        See the <a href="https://github.com/OpenLiberty/liberty-tools-intellij/compare/0.0.8...23.0.6">commit log</a> for the full set of changes since the previous release.
        <br>
        <h2> 0.0.8 </h2>
        Version 0.0.8 of Liberty Tools for IntelliJ IDEA is an <b>early release</b> that contains new functionality and fixes. Liberty Tools for IntelliJ IDEA now requires IntelliJ IDEA version 2022.2+ and Java 17+.
        <br>
        Notable enhancements:
        <ul>
        <li> Completion support when editing server.xml, server.env, bootstrap.properties via the <a href="https://github.com/OpenLiberty/liberty-language-server">Liberty Config Language Server</a>.
        <li> Completion support for Jakarta EE Web Profile 9.x APIs when editing Java files via the <a href="https://github.com/eclipse/lsp4jakarta">Eclipse LSP4Jakarta</a>, Language Server for Jakarta EE.
        <li> Completion support for MicroProfile 3 & 4 APIs when editing Java and microprofile-config.properties files via the <a href="https://github.com/eclipse/lsp4mp">Eclipse LSP4MP</a>, Language Server for MicroProfile.
        <li> Liberty projects are now automatically detected via the presence of a “src/main/liberty/config/server.xml” file.
        <li> New Liberty Run/Debug Configuration, accessible through the “Liberty: Start…” action or the IntelliJ Run/Debug Configuration menu. When “Debug” is selected, the Liberty runtime will start and the IntelliJ debugger will automatically attach to the JVM for the running Liberty instance.
        <li> Ability to run Liberty actions through the IntelliJ “Search Everywhere” window.
        <li> Ability to manually add Liberty projects to the Liberty tool window.
        <li> Liberty start actions will honour the Maven home path and Gradle JVM set in IntelliJ IDEA preferences.
        <li> New icons for Maven and Gradle projects in the Liberty tool window.
        </ul>
        See the <a href="https://github.com/OpenLiberty/liberty-tools-intellij/compare/0.0.7...0.0.8">commit log</a> for the full set of changes since the previous early release.
        <br>
        <h2> 0.0.7 </h2>
        <ul>
        <li> Rename plugin to Liberty Tools for IntelliJ
        </ul>
        <h2> 0.0.6 </h2>
        <ul>
        <li> Fix release
        </ul>
        <h2> 0.0.5 </h2>
        <ul>
        <li> Update action wording and documentation
        </ul>
        <h2> 0.0.4 </h2>
        <ul>
        <li> Removed tech preview wording from "start in container" action
        </ul>
        <h2> 0.0.3 </h2>
        <ul>
        <li> Updated plugin description
        </ul>
        <h2> 0.0.2 </h2>
        <ul>
        <li> Added start in container (tech preview) action
        </ul>
        <h2> 0.0.1 </h2>
        <ul>
        <li> First preview release
        </ul>
        """
    version project.version
}
